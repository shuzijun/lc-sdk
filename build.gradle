plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group 'com.shuzijun'
version (System.getenv('LD_VERSION')==null ? project.sdkVersion :System.getenv('LD_VERSION'))

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    api 'com.alibaba.fastjson2:fastjson2:2.0.24'
    api "com.squareup.okhttp3:okhttp:4.10.0"
    api "commons-io:commons-io:2.11.0"
    api 'org.apache.commons:commons-lang3:3.9'

    testImplementation "junit:junit:4.13.2"
    testRuntimeOnly "org.junit.vintage:junit-vintage-engine:5.8.2"
}

test {
    useJUnitPlatform()
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/shuzijun/lc-sdk")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
            pom({
                name = 'lc-sdk'
                packaging = 'jar'
                url = 'https://github.com/shuzijun/lc-sdk'

                licenses {
                    license {
                        name = 'GNU GENERAL PUBLIC LICENSE Version 3'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
            })
        }
    }
}
